# Name: Wang Shao
# ID: 520021911427

#Execution begins at address 0
    .pos 0
    irmovq stack, %rsp      #Set up stack pointer
    call main       #Execute main program
    halt            #Terminate program

#Sample linked list
.align 8
ele1:
    .quad 0x00a
    .quad ele2
ele2:
    .quad 0x0b0
    .quad ele3
ele3:
    .quad 0xc00
    .quad 0

main:
    irmovq ele1, %rdi
    call rsum   #rsum(ele1)
    ret

# rsm_list(list_ptr ls)
rsum:
    pushq %rbx
    pushq %rbp
    rrmovq %rdi, %rbx   #ls
    andq %rbx, %rbx     #Set CC
    je end              #s == NULL program end
    mrmovq (%rbx), %rbp #val
    mrmovq 8(%rbx), %rdi    #ls->next
    call rsum
    addq %rbp, %rax
    jmp release

end:
    irmovq $0, %rax
release:
    popq %rbp
    popq %rbx
    ret

# The stack starts here and grows to lower address
    .pos 0x200
stack:
